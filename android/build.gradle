buildscript {
    ext.safeExtGet = { prop, fallback ->
        rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    // Ensures backward compatibility
    ext.getKotlinVersion = {
        if (ext.has("kotlinVersion")) {
            ext.kotlinVersion()
        } else {
            ext.safeExtGet("kotlinVersion", "1.8.10")
        }
    }

    dependencies {
        // Matches recent template from React Native (0.67)
        // https://github.com/facebook/react-native/blob/0.67-stable/template/android/build.gradle#L16
        classpath("com.android.tools.build:gradle:${safeExtGet('gradlePluginVersion', '4.2.2')}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${getKotlinVersion()}")
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// Matches values in recent template from React Native 0.62
// https://github.com/facebook/react-native/blob/0.62-stable/template/android/build.gradle#L5-L8
def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_BUILD_TOOLS_VERSION = "28.0.3"
def DEFAULT_MIN_SDK_VERSION = 16
def DEFAULT_TARGET_SDK_VERSION = 28

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    buildToolsVersion safeExtGet('buildToolsVersion', DEFAULT_BUILD_TOOLS_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        dataBinding = true
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        // Matches recent template from React Native 0.62
        // https://github.com/facebook/react-native/blob/0.62-stable/template/android/build.gradle#L27
        url "$projectDir/../node_modules/react-native/android"
    }
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "com.facebook.react:react-native:${safeExtGet('reactnativeVersion', '+')}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${getKotlinVersion()}"

    // Tickets SDK
    implementation 'androidx.compose.material:material:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.ticketmaster.tickets:tickets:3.2.0'
    implementation 'com.ticketmaster.tickets:secure-entry:1.2.8'
    implementation "com.ticketmaster.retail:purchase:1.1.3"
    implementation "com.ticketmaster.retail:prepurchase:1.0.8"
    implementation "com.ticketmaster.retail:discoveryapi:1.0.3"
    implementation 'com.ticketmaster.accounts:authentication:3.2.0'
    implementation 'com.ticketmaster.retail:foundation:1.0.1'
}